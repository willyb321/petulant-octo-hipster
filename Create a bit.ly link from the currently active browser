on bitlyShorten(_text)
	
	try
		set login to "" -- Put your login name here
		set api_key to "" -- Put your API key here
		set the _encodedUrl to _urlEncode(_text) of me
		
		if login is "" or api_key is "" then
			set question to display dialog "Sorry, you need to open the script and provide your bitly username and api key. You can get them from: " & return & "https://bitly.com/a/your_api_key/" & return & "Open this location now?" giving up after 15
			if the button returned of the result is "OK" then
				tell application "Finder" to open location "https://bitly.com/a/your_api_key/"
				quit
			end if
		end if
		
		set curlCMD to ¬
			¬
				"curl --stderr /dev/null \"http://api.bit.ly/shorten?longUrl=" & _encodedUrl & "&history=1&version=2.0.1&login=" & login ¬
			& "&apiKey=" & api_key ¬
			& "\"| grep shortUrl | grep -o http.*[/a-zA-Z0-9]"
		
		tell me to set _bitlyUrl to (do shell script curlCMD)
		
		set AppleScript's text item delimiters to "http://"
		set _bitlyUrl to text item 3 of _bitlyUrl
		set AppleScript's text item delimiters to "\""
		set _bitlyUrl to "http://" & text item 1 of _bitlyUrl
		set AppleScript's text item delimiters to ""
		
		set the clipboard to _bitlyUrl
		
		
	on error a number b
		display dialog a
		
		return
	end try
end bitlyShorten

on _urlEncode(theText)
	set theTextEnc to ""
	repeat with eachChar in characters of theText
		set useChar to eachChar
		set eachCharNum to ASCII number of eachChar
		if eachCharNum = 32 then
			set useChar to "+"
		else if (eachCharNum ≠ 42) and (eachCharNum ≠ 95) ¬
			and (eachCharNum < 45 or eachCharNum > 46) ¬
			and (eachCharNum < 48 or eachCharNum > 57) ¬
			and (eachCharNum < 65 or eachCharNum > 90) ¬
			and (eachCharNum < 97 or eachCharNum > 122) then
			set firstDig to round (eachCharNum / 16) rounding down
			set secondDig to eachCharNum mod 16
			if firstDig > 9 then
				set aNum to firstDig + 55
				set firstDig to ASCII character aNum
			end if
			if secondDig > 9 then
				set aNum to secondDig + 55
				set secondDig to ASCII character aNum
			end if
			set numHex to ("%" & (firstDig as string) ¬
				& (secondDig as string)) as string
			set useChar to numHex
		end if
		set theTextEnc to theTextEnc & useChar as string
	end repeat
	return theTextEnc
end _urlEncode

on run
	
	tell application "System Events"
		set browserName to name of the first process whose frontmost is true
	end tell
	
	if browserName is "Google Chrome" then
		try
			tell application "Google Chrome"
				set shortenMe to (URL of active tab of first window)
			end tell
			bitlyShorten(shortenMe)
		on error
			display dialog "Problem shortening the URL from Chrome."
		end try
		
	else if browserName is "Safari" then
		try
			tell application "Safari"
				set shortenMe to URL of current tab of first window whose visible is true
			end tell
			bitlyShorten(shortenMe)
		on error
			display dialog "Problem shortening the URL from Safari."
		end try
		
	else
		try
			tell application browserName to activate
			delay 0.2
			set oldClipboard to the clipboard as string
			tell application "System Events"
				tell (first window whose visible is true)
					keystroke "l" using command down
					keystroke "c" using command down
				end tell
			end tell
			delay 0.2
			set shortenMe to the clipboard
			set the clipboard to oldClipboard
			bitlyShorten(shortenMe)
			
		on error
			display dialog "Problem shortening the URL from " & browserName & ". Tried using UI Scripting to command L, command C."
		end try
	end if
	
end run
